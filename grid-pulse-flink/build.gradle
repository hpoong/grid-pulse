plugins {
	id 'java'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.hopoong'
version = '0.0.1-SNAPSHOT'

dependencies {
	// Flink Core
	implementation "org.apache.flink:flink-streaming-java:1.20.2"
	implementation "org.apache.flink:flink-clients:1.20.2"

	// Kafka Connector - 반드시 포함!
	implementation "org.apache.flink:flink-connector-kafka:3.3.0-1.20"
	implementation "org.apache.flink:flink-connector-base:1.20.2"

	// JSON 처리
	implementation "org.apache.flink:flink-json:1.20.2"
	implementation "com.fasterxml.jackson.core:jackson-databind:2.17.0"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.0"
}

// UTF-8 인코딩
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

// ShadowJar 설정
shadowJar {
	archiveClassifier.set('')
	
	manifest {
		attributes(
			'Main-Class': 'com.hopoong.flink.GridPulseFlinkApplication'
		)
	}
	
	// META-INF 서명 파일 제외
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
	
	// 서비스 파일 병합
	mergeServiceFiles()
	
	// ZIP64 지원 (큰 파일)
	zip64 true
}

// build 실행 시 shadowJar도 실행
build {
	dependsOn shadowJar
}

test {
	useJUnitPlatform()
}